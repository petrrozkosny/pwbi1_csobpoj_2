01 suma srazek = 
-- scenar 1: Respektuji filtry vizualu, zadne nepridavam,
-- tj. pouziji zakladni agregacni funkce typu SUM(), MAX(), MIN()
SUM(data[srazky])

_____________________________

02 suma srazek ruzyne = 

-- scenar2: Respektuji filtry vizualu a pridavam dalsi
-- tj. pouziji kombinaci funkci SUMX(FILTER()),MAXX(FILTER()), MINX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data s jejimi filtry
    data,
    --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
    data[lokalita] = "RUZYNE" ),
    --krok4: Funkce SUMX() scita sloupec srazky tabulky
    -- vracene funkci FILTER()
    data[srazky])
    
    ____________________________________
    
    03 suma srazek vsechny roky = 

-- scenar3: Rusim filtry vizualu a zadne nenastavuji,
-- tj. pouziji kombinaci funkci SUMX(ALL()), MINX(ALL()), MAXX(ALL()) apod.

SUMX(
    -- krok1: Funkce ALL() vraci kopii tabulky data zbavenou vsech filtru
    ALL(data),
    -- krok2: Funkce SUMX() agraguje sloupec srazky tabulky vracene
    -- funkci ALL()
    data[srazky])


________________________________________

04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, tj. pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MAXX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
        -- krok1: Funkce ALL() vratii kopii tabulky data zbavenou
        -- vsech filtru
        ALL(data),
    -- krok2: Funkce FILTER() nastavuje filtr na sloupec rok
    -- tabulky vracene funkci ALL()
    data[rok] = 2000),
-- krok4: Funkce SUMX() scita sloupec srazky tabulky vracene funkci FILTER()
data[srazky])


_______________________________

05 suma srazek mezirocne = 

IF(SELECTEDVALUE(data[rok]) = MINX(ALL(data),data[rok]),BLANK(),
-- suma srazek za aktualni rok
sum(data[srazky])

-- M√çNUS
-
-- suma srazek za predchozi rok
SUMX(
    FILTER(
        ALL(data),
    data[rok] = SELECTEDVALUE(data[rok]) - 1),
data[srazky])   )



___________________________________________________________

07 suma srazek v maximalnim roce lokality = 
-- suma srazek v dane lokalite v maximalnim roce dane lokality

SUMX(
    FILTER(data,
    data[rok] = MAX(data[rok])),
data[srazky])

______________________________

08 suma srazek v minimalnim roce ze vsech lokalit = 
SUMX(
    FILTER(data,
    data[rok] = 
    MINX(ALL(data),data[rok])),
data[srazky])
_______________________

09 podil srazek lokality na srazkach vsech lokalit = 
VAR vypocet1 = 
-- suma srazek v dane lokalite
SUM(data[srazky])
/
-- suma srazek ve vsech lokalitach
SUMX(
    ALL(data),
data[srazky])

VAR vypocet2 =
IFERROR(
[01 suma srazek] / [03 suma srazek vsechny roky],BLANK())

RETURN vypocet2


__________________________________________

11 rok maximalni teploty v dane lokalite = 
-- maximalni rok maximalni teploty

MAXX(
    FILTER(data,
    data[maximalni_teplota] = 
    -- maximalni teplote v dane lokalite
    MAX(data[maximalni_teplota])),
data[rok])


____________________________________

16 max datum max teploty v max roce = 
-- maximalni rok pro danou lokalitu
VAR max_rok = MAX(data[rok])
-- maximalni teplota v maximalnim roce dane lokality =

VAR max_teplota = 
MAXX(
    FILTER(data,
    data[rok] = max_rok),
data[maximalni_teplota])

RETURN  
MAXX(
    FILTER(data,
    data[rok] = max_rok
    && data[maximalni_teplota] = max_teplota),
data[datum])


___________________________

12 pocet dnu bez srazek v dane lokalite = 

-- srazky = 0

COUNTX(
    FILTER(data,
    data[srazky] = 0),
data[datum])

______________________________

17 podil nedestivych dnu = 
COUNTX(
    FILTER(data,
    data[srazky] = 0
    && NOT ISBLANK(data[srazky])
    ),
data[datum]) /

COUNT(data[datum])

_________________________________________

18 suma srazek ruzyne = 

SUMX(
    FILTER(data,
    data[lokalita] = "RUZYNE"),
data[srazky])

___________________________________

19 suma srazek predchozi rok = 
IF(ISFILTERED(data[rok]),
SUMX(
    FILTER(
        ALL(data),
    data[lokalita] = "RUZYNE"
    && data[mesic] = SELECTEDVALUE(data[mesic])
    && data[rok] = SELECTEDVALUE(data[rok]) - 1),
data[srazky]))

______________________________________

20 suma srazek kumulativne v ruzyni = 

SUMX(
    FILTER(
        ALL(data),
    data[lokalita] = "RUZYNE"
    && data[rok] = SELECTEDVALUE(data[rok])
    && data[mesic] <= SELECTEDVALUE(data[mesic])),
data[srazky])

_________________________________________________________________________

dim_datumy = 

ADDCOLUMNS(
    SELECTCOLUMNS(
        CALENDARAUTO(),
    "datum",[Date]),
"kvartal",QUARTER([datum]))

_____________________________________________________

typ_hodnoty = 
UNION(
    ROW("hodnota","prubezne"),
    ROW("hodnota","kumulativne")
)


_________________________________________

21 suma srazek v danem roce napric lokalitami = 

-- varianta1: TAKTO NE
VAR _1 =
    CALCULATE(
        SUM(data[srazky]),
        FILTER(
            ALL(data),
        data[rok] = SELECTEDVALUE(data[rok])))
-- varianta2: Takto ano
VAR _2 = 
    CALCULATE(
        SUM(data[srazky]),
        ALL(data[lokalita]))

RETURN _2
_________________________________________

22 suma srazek v prechozim roce v dane lokalite = 
-- takto v pripade, ze nefunguje varianta 2
VAR _1 = 
CALCULATE(
        SUM(data[srazky]),
        FILTER(
            ALL(data[rok]),
        data[rok] = SELECTEDVALUE(data[rok]) - 1))

VAR _2 = 

CALCULATE(
    SUM(data[srazky]),
 
   data[rok] = SELECTEDVALUE(data[rok]) - 1)

return _2

_______________________________________________________

23 suma srazek v zemi = 

CALCULATE(
    SUM(data[srazky]),  
    USERELATIONSHIP(data[lokalita],dim_oblasti[stanice]))
